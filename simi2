#!/usr/bin/env perl

use warnings FATAL => 'all';
use strict;
use 5.10.0;

# FIXME: invert subdiffs (when should subdiffing be done?)
# FIXME: make set/output object oriented

##############################################################################

# FIXME: add note about missing newline at end
sub read_file {
    my ($file) = @_;
    open my $in, $file or die "Cannot open file '$file' for reading: $!\n";
    my @data = <$in>;
    close $in, $file or die "Cannot close file '$file' after reading: $!\n";
    return wantarray
        ? do { s/\r?\n\z// foreach @data; @data }
        : join "", @data;
}

{
    my %opt = (
        '+' => "",
        '-' => "",
        ""  => "",
        reset => "",
        old_width => 3,
        new_width => 3,
    );

    sub set {
        my %arg = @_;
        foreach (keys %arg) {
            $opt{$_} = $arg{$_};
        }
    }

    sub output {
        my ($change, $old, $new, $string) = @_;
        printf(
            "%$opt{old_width}s %$opt{new_width}s $opt{$change}%1s %s$opt{reset}\n",
            $old,
            $new,
            $change,
            $string,
        );
    }
}


##############################################################################

my %opt = (
    color => 1,
    all   => 0,
);

if (@ARGV != 2) {
    die "$0: Bad number of arguments\n",
        "Usage: diffy FILE1 FILE2\n";
}


my @old = read_file shift;
my @new = read_file shift;
my ($old, $new) = (1, 1);                      # line count

use Algorithm::Diff 'sdiff';
my @sdiff = sdiff(\@old, \@new);

set(
    old_width => length scalar @old,
    new_width => length scalar @new,
);

if ($opt{color}) {
    set(
        '+' => "\e[32m",
        '-' => "\e[31m",
        reset => "\e[m"
    );
}

my $line = 0;
foreach (@sdiff) {
    my ($change, $oldstr, $newstr) = @$_;

    if ($change eq "u") {                      # unchanged
        if ($opt{all}) {
            output("", $old, $new, $oldstr);
        } elsif (!$line) {
            say "------------------------------";
            $line = 1;
        }
        $old += 1;
        $new += 1;
        next;
    }
    $line = 0;
    if ($change eq "c") {                      # changed
        output("-", $old, "", $oldstr);
        output("+", "", $new, $newstr);
        $old += 1;
        $new += 1;
    } elsif ($change eq "+") {                 # added
        output("+", "", $new, $newstr);
        $new += 1;
    } elsif ($change eq "-") {                 # removed
        output("-", $old, "", $oldstr);
        $old += 1;
    }
}

#[eof]
